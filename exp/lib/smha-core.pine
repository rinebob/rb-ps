// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © rinebob

//@version=6
library("rbSmHaCore", overlay=false)

// smha_fast_slow_src
// -------------------
// Generic bar-level smoothed Heiken Ashi band engine. This is the single
// source of truth for smHA math. It operates on arbitrary OHLC sources and
// length parameters expressed in bars.
//
// Parameters:
//   src_open, src_high, src_low, src_close - series float, input OHLC
//   smooth_length       - int, smoothing length before HA calculation (bars)
//   after_smooth_length - int, smoothing length after HA calculation (bars)
//
// Returns:
//   [o, h, l, c, m, up, dn, closeCrossesAboveHA, closeCrossesBelowHA]
export smha_fast_slow_src(
    series float src_open,
    series float src_high,
    series float src_low,
    series float src_close,
    simple int  smooth_length,
    simple int  after_smooth_length) =>
    float haopen = na

    int beforeLength = smooth_length
    int afterLength  = after_smooth_length

    // Pre-smooth OHLC
    o = ta.ema(src_open,  beforeLength)
    h = ta.ema(src_high,  beforeLength)
    l = ta.ema(src_low,   beforeLength)
    c = ta.ema(src_close, beforeLength)
    series float m = 0.0

    // Heiken Ashi on smoothed OHLC
    haclose = (o + h + l + c) / 4.0
    haopen := na(haopen[1]) ? (o + c) / 2 : (haopen[1] + haclose[1]) / 2
    hahigh = math.max(h, math.max(haopen, haclose))
    halow  = math.min(l, math.min(haopen, haclose))

    // Post-smooth HA OHLC
    o := ta.ema(haopen,  afterLength)
    h := ta.ema(hahigh,  afterLength)
    l := ta.ema(halow,   afterLength)
    c := ta.ema(haclose, afterLength)

    // Force proper candle bodies and compute midpoint
    h := o > c ? o : c
    l := o > c ? c : o
    m := l + (math.abs(h - l) / 2)

    up = c > o
    dn = o > c

    closeCrossesAboveHA = ta.crossover(src_close, h)
    closeCrossesBelowHA = ta.crossunder(src_close, l)

    [o, h, l, c, m, up, dn, closeCrossesAboveHA, closeCrossesBelowHA]


// smha_fast_slow
// ----------------
// Chart-timeframe wrapper around the generic engine. Uses chart OHLC and
// lengths expressed in chart bars.
export smha_fast_slow(simple int smooth_length, simple int after_smooth_length) =>
    smha_fast_slow_src(open, high, low, close, smooth_length, after_smooth_length)